plugins {
    id 'application'
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'nu.studer.jooq' version '8.2'
    id 'com.google.protobuf' version '0.9.4'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

group 'com.jakub.bone'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // JUnit
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    // Mockito
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'

    // Log4j
    implementation 'org.apache.logging.log4j:log4j-core:2.23.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.8'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    //JavaFX
    implementation 'org.openjfx:javafx-controls:21.0.4'
    implementation 'org.openjfx:javafx-fxml:21.0.4'
    implementation 'org.openjfx:javafx-graphics:21.0.4'

    // JOOQ
    implementation 'org.jooq:jooq:3.19.15'
    implementation 'org.postgresql:postgresql:42.5.4'
    jooqGenerator 'org.postgresql:postgresql:42.5.4'

    // jim3dsModelImporterJFX
    implementation files('libs/jim3dsModelImporterJFX/jimObjModelImporterJFX.jar')

    // Jetty
    implementation 'org.eclipse.jetty:jetty-server:11.0.15'
    implementation 'org.eclipse.jetty:jetty-servlet:11.0.15'

    // gRPC
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.49.2'
    implementation 'io.grpc:grpc-protobuf:1.49.2'
    implementation 'io.grpc:grpc-stub:1.49.2'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.61.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

jooq {
    version = '3.19.15'
    configurations {
        main {
            generationTool {
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5432/airport_system'
                    user = 'airport'
                    password = 'plane123'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        includes = '.*'
                        excludes = ''
                        inputSchema = 'public'
                    }
                    target {
                        packageName = 'jooq'
                        directory = 'src/main/java/com/jakub/bone/generated'
                    }
                }
            }
        }
    }
}


application {
    mainClass = 'simulation.SimulationApp'
}

test {
    useJUnitPlatform()
}




